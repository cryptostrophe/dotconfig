#!/usr/bin/env fish

if not command -sq code
  echo >&2 "error: VS Code not found"
  exit 1
end

set extensions_file ~/.config/Code/User/extensions
set desired_extensions (cat $extensions_file)
set installed_extensions (code --list-extensions); or exit

function do_install
  for extension in $desired_extensions
    if not contains $extension $installed_extensions
      code --install-extension $extension
    end
  end
end

function do_cleanup
  for extension in $installed_extensions
    if not contains $extension $desired_extensions
      code --uninstall-extension $extension
    end
  end
end

function do_list
  printf "%s\\n" $installed_extensions
end

function do_diff
  diff $extensions_file (do_list | psub)
end

switch "$argv[1]"
case install cleanup list diff
  eval do_$argv[1]
case sync
  do_install
  do_cleanup
case dump
  do_list > $extensions_file
case ""
  echo "Subcommands: cleanup diff dump install list sync"
case "*"
  echo >&2 "error: don't know how to $argv[1]"
  exit 1
end

# vim: set ft=fish
