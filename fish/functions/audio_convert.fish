function audio_convert --description 'Convert audio between various formats' -a in_file out_file
  set -l fmt
  switch "$out_file"
    case "*.3gp"
      set fmt "3gpp"
    case "*.3g2"
      set fmt "3gp2"
    case "*.aac" "*.adts"
      set fmt "adts"
    case "*.ac3"
      set fmt "ac-3"
    case "*.aifc"
      set fmt "AIFC"
    case "*.aiff" "*.aif"
      set fmt "AIFF"
    case "*.amr"
      set fmt "amrf"
    case "*.m4a" "*.m4r"
      set fmt "m4af"
    case "*.m4b"
      set fmt "m4bf"
    case "*.caf"
      set fmt "caff"
    case "*.ec3"
      set fmt "ec-3"
    case "*.flac"
      set fmt "flac"
    case "*.mp1"
      set fmt "MPG1"
    case "*.mp2"
      set fmt "MPG2"
    case "*.mp3" "*.mpeg"
      set fmt "MPG3"
    case "*.mp4"
      set fmt "mp4f"
    case "*.snd" "*.au"
      set fmt "NeXT"
    case "*.sd2"
      set fmt "Sd2f"
    case "*.wav"
      set fmt "WAVE"
    case "*"
      echo >&2 "error: unknown output type:" (string split . "$out_file")[-1]
      return 1
  end

  afconvert -f "$fmt" "$in_file" "$out_file"
end
